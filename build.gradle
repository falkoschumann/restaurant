plugins {
  id 'checkstyle'
  id 'java'
  id 'jacoco'
  id 'com.diffplug.spotless' version '6.13.0'
  id 'org.sonarqube' version '3.5.0.2730'
  id 'org.springframework.boot' version '3.0.1'
  id 'io.freefair.lombok' version '6.6.1'
  id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'de.muspellheim'
version = '0.0.1-SNAPSHOT'

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-jdbc'
  implementation 'org.postgresql:postgresql:42.5.1'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.hsqldb:hsqldb:2.7.1'
}

compileJava {
  options.release = 17
  options.encoding = 'utf-8'
  options.compilerArgs.addAll(['-Werror', '-implicit:class'])
}

test {
  useJUnitPlatform()
  if (System.properties['skipIntegrationTests'] != null) {
    println '***** Skip integration tests !!! *****'
    excludes = ['**/*IntegrationTests*']
  }
  testLogging {
    events 'passed', 'skipped', 'failed'
    showExceptions = true
    exceptionFormat 'full'
  }
  finalizedBy jacocoTestReport
}

compileTestJava {
  options.release = 17
  options.encoding = 'utf-8'
  options.compilerArgs.addAll(['-Werror', '-implicit:class'])
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = true
  }
}

checkstyle {
  var archive = configurations.checkstyle.filter {
    it.name.startsWith("checkstyle")
  }
  config = resources.text.fromArchiveEntry(archive, "google_checks.xml")
  configProperties 'org.checkstyle.google.suppressionfilter.config': file('config/checkstyle/suppressions.xml')
  maxWarnings = 0
}

spotless {
  java {
    googleJavaFormat()
  }
}

sonarqube {
  properties {
    property "sonar.projectKey", "falkoschumann_restaurant"
    property "sonar.organization", "falkoschumann"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}
